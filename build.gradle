plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'com.gradleup.shadow' version '8.3.8'
}

group = 'me.devupdates'
version = '2.0.1'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
    maven {
        name = "placeholderapi"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        name = "nexus"
        url = "https://nexus.hc.to/content/repositories/pub_releases/"
    }
    maven {
        name = "sk89q-repo"
        url = "https://maven.enginehub.org/repo/"
    }
    maven {
        name = "mcmmo-repo"
        url = "https://nexus.neetgames.com/repository/maven-public/"
    }
    maven {
        name = "bg-software"
        url = "https://repo.bg-software.com/repository/api/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT")
    
    // Vault API
    compileOnly("net.milkbowl.vault:VaultAPI:1.7")
    
    // JobsReborn API
    compileOnly("com.github.Zrips:Jobs:v5.2.6.2") {
        exclude group: 'uk.antiperson.stackmob', module: 'StackMob'
        exclude group: 'com.bgsoftware', module: 'WildStackerAPI'
        exclude group: 'com.gmail.nossr50.mcMMO', module: 'mcMMO'
        exclude group: 'com.sk89q.worldguard', module: 'worldguard-bukkit'
        exclude group: 'com.sk89q.worldedit', module: 'worldedit-bukkit'
        exclude group: 'net.coreprotect', module: 'coreprotect'
        exclude group: 'com.massivecraft', module: 'factions'
        exclude group: 'com.github.TechFortress', module: 'GriefPrevention'
        exclude group: 'com.bekvon.bukkit.residence', module: 'Residence'
        exclude group: 'com.palmergames.bukkit.towny', module: 'towny'
        exclude group: 'com.github.MilkBowl', module: 'VaultAPI'
        exclude group: 'me.clip', module: 'placeholderapi'
    }
    
    // PlaceholderAPI (Optional)
    compileOnly("me.clip:placeholderapi:2.11.6")
    
    // SQLite database driver - Not shaded, will be downloaded at runtime
    compileOnly("org.xerial:sqlite-jdbc:3.45.0.0")
    
    // bStats metrics
    implementation("org.bstats:bstats-bukkit:3.1.0")
}

// Simple shadowJar configuration that works
tasks {
    shadowJar {
        archiveClassifier.set('')
        
        // Relocate bStats
        relocate('org.bstats', 'me.devupdates.vaultPayday.libs.bstats')
        
        mergeServiceFiles()
    }
    
    build {
        dependsOn(shadowJar)
    }
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

